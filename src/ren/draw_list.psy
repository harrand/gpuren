// create a new draw list
// initial_maximum_draw_count => initial capacity of draws before having to resize internal buffers
impl_ren_create_draw_list ::= func(info : ren_draw_list_info -> ren_draw_list)
{
	initial_maximum_draw_count ::= 8092;
	ib ::= gpu_create_buffer(gpu_resource_info
	{
		.data := zero;
		.data_size := __sizeof(u32) + (__sizeof(gpu_draw_indexed_command) * initial_maximum_draw_count);
		.flags := gpu_resource_flag.zero_memory;
		.buffer_flags := gpu_buffer_flag.indirect_buffer;
		.name := "draw buffer";
	}, global_arena);
	initial_free_list_cap ::= 64;
	return ren_draw_list
	{
		.indirect_buffer := ib.res;
		.count := 0;
		.free_list := arena_alloc(global_arena, __sizeof(u64) * initial_free_list_cap);
		.free_list_count := 0;
		.free_list_cap := initial_free_list_cap;
	};
};

impl_ren_draw_list_set_mesh ::= func(d : ren_draw_list mut?, h : ren_draw_handle, mesh : mesh_locator -> v0)
{
	idx ::= _out_draw_handle(h);
	cmd ::= gpu_draw_indexed_command
	{
		.index_count := mesh.ilen@_;
		.instance_count := 1;
		.first_index := mesh.ioff@_;
		.vertex_offset := mesh.voff@_;
		.first_instance := 0;
	};
	indirect_offset ::= __sizeof(u32) + (idx * __sizeof(gpu_draw_indexed_command));
	gpu_resource_write(d->indirect_buffer, ref cmd, __sizeof(gpu_draw_indexed_command), indirect_offset);
};

impl_ren_draw_list_add_mesh ::= func(d : ren_draw_list mut?, mesh : mesh_locator -> ren_draw_handle)
{
	idx : u64 mut := 0;
	if(d->free_list_count > 0)
	{
		// pop back off of free list
		d->free_list_count = ((d->free_list_count) - 1);
		idx = (d->free_list_count);
	}
	else
	{
		idx = d->count;
		d->count = (d->count + 1);
	}

	ret ::= _in_draw_handle(idx);
	ren_draw_list_set_mesh(d, ret, mesh);

	len32 ::= d->count@u32;
	gpu_resource_write(d->indirect_buffer, ref len32, __sizeof(len32), 0);

	return ret;
};

impl_ren_draw_list_remove ::= func(d : ren_draw_list mut?, h : ren_draw_handle -> v0)
{
	impl_draw_list_set_degenerate(d, h);
	while((d->free_list_count) >= (d->free_list_cap))
	{
		olddata ::= d->free_list;
		oldcap ::= d->free_list_cap;
		d->free_list_cap = (d->free_list_cap * 2);
		d->free_list = arena_alloc(global_arena, d->free_list_cap * __sizeof(u64));
		memcopy(d->free_list, olddata, oldcap * __sizeof(u64));
	}
	deref(d->free_list # (d->free_list_count)) = _out_draw_handle(h);
	d->free_list_count = (d->free_list_count + 1);
};

impl_ren_draw_list_count ::= func(d : ren_draw_list mut? -> u64)
{
	return d->count;
};

impl_ren_draw_list_capacity ::= func(d : ren_draw_list mut? -> u64)
{
	capacity_incl_indirect_count_bytes ::= gpu_resource_size(d->indirect_buffer);
	return (capacity_incl_indirect_count_bytes - __sizeof(u32)) / __sizeof(gpu_draw_indexed_command);
};

_in_draw_handle ::= func(idx : u64 -> ren_draw_handle)
{
	return (idx + 1)@ren_draw_handle;
};

_out_draw_handle ::= func(handle : ren_draw_handle -> u64)
{
	return ((handle)@s64 - 1)@_;
};

impl_draw_list_set_degenerate ::= func(d : ren_draw_list mut?, h : ren_draw_handle -> v0)
{
	ren_draw_list_set_mesh(d, h, zero@mesh_locator);
};
